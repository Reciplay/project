plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.3'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
}

group = 'com.e104'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.h2database:h2'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'org.mockito:mockito-core'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	// https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-test
	testImplementation("org.springframework.boot:spring-boot-starter-test:3.5.3")
	// https://mvnrepository.com/artifact/com.mysql/mysql-connector-j
	implementation("com.mysql:mysql-connector-j:9.3.0")

	//Querydsl
	implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
	annotationProcessor 'com.querydsl:querydsl-apt:5.0.0:jakarta'
	annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
	annotationProcessor 'jakarta.persistence:jakarta.persistence-api'


	//live서버 의존성 추가
	implementation 'org.springframework.boot:spring-boot-starter-websocket' // WebSocket (STOMP)
	implementation 'io.livekit:livekit-server:0.10.0'// LiveKit Server SDK
	implementation 'com.auth0:java-jwt:4.4.0' // 또는 최신 버전
	// https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-test
	testImplementation("org.springframework.boot:spring-boot-starter-test:3.5.3")

	
	// SpringDoc OpenAPI for Swagger UI (Spring Boot 3 compatible)
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0'

	// jwt 의존성
	implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
	implementation 'io.jsonwebtoken:jjwt-impl:0.12.3'
	implementation 'io.jsonwebtoken:jjwt-jackson:0.12.3'

	// aws 의존성, s3 의존성
	// https://mvnrepository.com/artifact/org.springframework.cloud/spring-cloud-starter-aws
	implementation("org.springframework.cloud:spring-cloud-starter-aws:2.2.6.RELEASE")
	// https://mvnrepository.com/artifact/software.amazon.awssdk/s3
	implementation("software.amazon.awssdk:s3:2.32.6")


	// 파일 경로/이름/확장자 처리용 Apache Commons IO 유틸리티(25.08.01 지욱)
	implementation("commons-io:commons-io:2.11.0")
	// AWS SDK BOM: S3 등 AWS 모듈 버전 통일 관리(25.08.01 지욱)
	implementation platform('software.amazon.awssdk:bom:2.25.12')

	// redis 의존성
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'

	// 메일 의존성.
	implementation 'org.springframework.boot:spring-boot-starter-mail'

	// oauth2.0 의존성
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

	// OIDC(ID Token) 파싱/검증용 (구글 등): Nimbus 기반
	implementation 'org.springframework.security:spring-security-oauth2-jose'

	// webflux 의존성
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
}

def generated = layout.buildDirectory.dir("generated/querydsl").get().asFile;

sourceSets {
	main {
		java {
			srcDirs += [generated]
		}
	}
}

tasks.withType(JavaCompile).configureEach {
	options.getGeneratedSourceOutputDirectory().set(file(generated))
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy 'jacocoTestReport'
}

// jacoco report 설정
jacocoTestReport {
	reports {
		// html로 report 생성하기
		// 빌드경로/jacoco/report.html 폴더 내부로 경로 설정
		html.destination file("$buildDir/jacoco/report.html")
	}

	// jacocoTestReport가 끝나면 jacocoTestCoverageVerification 동작
	finalizedBy 'jacocoTestCoverageVerification'
}

// jacoco 커버리지 검증 설정
jacocoTestCoverageVerification {
	violationRules {
		rule {
			enabled = true // 커버리지 적용 여부
			element = 'CLASS' // 커버리지 적용 단위

			// 라인 커버리지 설정
			// 적용 대상 전체 소스 코드들을 한줄 한줄 따졌을 때 테스트 코드가 작성되어 있는 줄의 빈도
			// 테스트 코드가 작성되어 있는 비율이 90% 이상이어야 함
			limit {
				counter = 'LINE'
				value = 'COVEREDRATIO'
				minimum = 0.90
			}

			// 브랜치 커버리지 설정
			// if-else 등을 활용하여 발생되는 분기들 중 테스트 코드가 작성되어 있는 빈도
			// 테스트 코드가 작성되어 있는 비율이 90% 이상이어야 함
			limit {
				counter = 'BRANCH'
				value = 'COVEREDRATIO'
				minimum = 0.90
			}

			// 라인 최대 갯수 설정
			// 빈 줄을 제외하고 하나의 자바 파일에서 작성될 수 있는 최대 라인 갯수
			// 한 파일에 최대 500줄까지 작성되어야 함
			limit {
				counter = 'LINE'
				value = 'TOTALCOUNT'
				maximum = 500
			}
		}
	}
}

tasks.register("compileQuerydsl", JavaCompile) {
	group = "build"
	description = "Generate QueryDSL Q-classes"
	source = sourceSets.main.java
	classpath = sourceSets.main.compileClasspath
	destinationDirectory.set(file(generated))

	options.annotationProcessorPath = configurations.annotationProcessor
}

clean {
    delete file(generated)
}